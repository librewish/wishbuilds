# Maintainer: Philip MÃ¼ller <philm@garuda.org>
# Maintainer: Bernhard Landauer <oberon@garuda.org>
# Maintainer: Frede Hundewadt <fhatgarudarg>

_pkgbase=garuda-tools
_ver=0.15.6
#_branch=stable-0.15.x
_branch=master

pkgbase=garuda-tools-git
pkgname=('garuda-tools-base-git'
        'garuda-tools-pkg-git'
        'garuda-tools-iso-git'
        'garuda-tools-yaml-git')
pkgver=r4.eba2c75
pkgrel=1
arch=('x86_64' 'i686')
pkgdesc='Development tools for garuda Linux'
license=('GPL')
groups=('garuda-tools')
url='https://gitlab.com/garuda-linux/tools/garuda-tools'
makedepends=('git' 'docbook2x')
source=("garuda-tools::git+$url.git#branch=$_branch")
sha256sums=('SKIP')

prepare() {
    cd ${srcdir}/${_pkgbase}
    # patches here
    sed -e "s/^Version=.*/Version=$_ver/" -i Makefile
    #sed -e "s/^Version=.*/Version=$pkgver/" -i Makefile
        if [ "${CARCH}" = "i686" ]; then
        patch -p2 -i ${srcdir}/garuda-32.patch
    fi
}

pkgver() {
    cd ${srcdir}/${_pkgbase}
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd ${srcdir}/${_pkgbase}
    make SYSCONFDIR=/etc PREFIX=/usr
}

package_garuda-tools-base-git() {
    pkgdesc='Development tools for garuda Linux (base tools)'
    provides=("garuda-tools-base=$_ver")
    depends=('openssh' 'rsync' 'haveged' 'os-prober' 'gnupg' 'pacman-mirrorlist>=20160301')
    optdepends=('garuda-tools-pkg: garuda Linux package tools'
                'garuda-tools-iso: garuda Linux iso tools'
                'garuda-tools-yaml: garuda Linux yaml tools')
    conflicts=('garuda-tools-base')
    backup=('etc/garuda-tools/garuda-tools.conf')

    cd ${srcdir}/${_pkgbase}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_garuda-tools-pkg-git() {
    pkgdesc='Development tools for garuda Linux (packaging tools)'
    provides=("garuda-tools-pkg=$_ver")
    depends=('namcap' 'garuda-tools-base-git')
    conflicts=('garuda-tools-pkg' 'devtools')

    cd ${srcdir}/${_pkgbase}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_pkg
}

package_garuda-tools-yaml-git() {
    pkgdesc='Development tools for garuda Linux (yaml tools)'
    provides=("garuda-tools-yaml=$_ver")
    conflicts=('garuda-tools-yaml')
    depends=('garuda-tools-base-git' 'calamares-tools' 'ruby-kwalify')

    cd ${srcdir}/${_pkgbase}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_yaml
}

package_garuda-tools-iso-git() {
    pkgdesc='Development tools for garuda Linux (ISO tools)'
	provides=("garuda-tools-iso=$_ver")
    depends=('dosfstools' 'libisoburn' 'squashfs-tools' 'garuda-tools-yaml-git'
            'mkinitcpio' 'mktorrent' 'grub' 'snapd' 'git')
	conflicts=('garuda-tools-iso')
    optdepends=('virtualbox: quickly test isos')

	cd ${srcdir}/${_pkgbase}
	make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_iso
}

